import com.vdurmont.semver4j.Semver
import com.vdurmont.semver4j.Semver.SemverType
import io.github.nstdio.http.ext.ReadmeUpdateTask
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

buildscript {
    dependencies {
        classpath 'com.vdurmont:semver4j:3.1.0'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id 'org.sonarqube' version '3.3'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "net.researchgate.release" version "2.8.1"
    id "de.jjohannes.extra-java-module-info"
}

group 'io.github.nstdio'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

def isCI = Boolean.parseBoolean(System.getenv("CI"))

ext {
    junitVersion = '5.8.2'
    commonIoVersion = '1.3.2'
    assertJVersion = '3.22.0'
    jsonPathAssertVersion = '2.7.0'
    slf4jVersion = '1.7.36'
    jacksonVersion = '2.13.2'
    brotli4JVersion = "1.6.0"
    brotliOrgVersion = "0.1.2"

    spiDeps = ["org.brotli:dec:$brotliOrgVersion", "com.aayushatharva.brotli4j:brotli4j:$brotli4JVersion"]
}

java {
    sourceSets {
        spiTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            java {}
        }
    }
}

configurations {
    spiTestImplementation.extendsFrom testImplementation
    spiTestRuntimeOnly.extendsFrom testRuntimeOnly

    configure([testRuntimeClasspath, testCompileClasspath, spiTestRuntimeClasspath, spiTestCompileClasspath], {
        attributes { attribute(Attribute.of("javaModule", Boolean), false) }
    })
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    compileOnly spiDeps

    /** AssertJ & Friends */
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "com.jayway.jsonpath:json-path-assert:$jsonPathAssertVersion"

    testImplementation "org.apache.commons:commons-io:$commonIoVersion"

    /** Jupiter */
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation 'org.mockito:mockito-core:4.4.0'

    testImplementation "com.github.tomakehurst:wiremock-jre8:2.32.0"
    testImplementation 'com.tngtech.archunit:archunit-junit5:0.23.1'

    spiTestImplementation spiDeps
    spiTestImplementation("com.aayushatharva.brotli4j:native-${getArch()}:$brotli4JVersion")
}

static def getArch() {
    def operatingSystem = DefaultNativePlatform.getCurrentOperatingSystem()

    if (operatingSystem.isWindows()) "windows-x86_64"
    else if (operatingSystem.isMacOsX()) "osx-x86_64"
    else if (operatingSystem.isLinux())
        if (DefaultNativePlatform.getCurrentArchitecture().isArm()) "linux-aarch64"
        else "linux-x86_64"
}

sonarqube {
    properties {
        property "sonar.projectKey", "nstdio_http-client-ext"
        property "sonar.organization", "nstdio"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'JDK Http Client Extensions'
                description = 'The set of usefult extensions for JDK HttpClient.'
                url = 'https://github.com/nstdio/http-client-ext'

                scm {
                    connection = 'scm:git:git@github.com:nstdio/http-client-ext.git'
                    developerConnection = 'scm:git:git@github.com:nstdio/http-client-ext.git'
                    url = 'https://github.com/nstdio/http-client-ext'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'nstdio'
                        name = 'Edgar Asatryan'
                        email = 'nstdio@gmail.com'
                    }
                }

            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            def urlBase = "https://s01.oss.sonatype.org"
            nexusUrl = uri("$urlBase/service/local/")
            snapshotRepositoryUrl = uri("$urlBase/content/repositories/snapshots/")
        }
    }
}

signing {
    required { !version.endsWith("SNAPSHOT") }

    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

release {
    tagTemplate = 'v${version}'

    git {
        requireBranch = 'main'
        pushToRemote = 'origin'
    }
}

tasks.register('updateReadme', ReadmeUpdateTask)

afterEvaluate {
    afterReleaseBuild.dependsOn publishToSonatype, closeAndReleaseSonatypeStagingRepository, updateReadme
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }

    finalizedBy jacocoTestReport
}

task spiTest(type: Test) {
    description = "Run SPI tests"
    group = "verification"
    testClassesDirs = sourceSets.spiTest.output.classesDirs
    classpath = sourceSets.spiTest.runtimeClasspath
}

check.dependsOn spiTest
build.dependsOn spiTest

extraJavaModuleInfo {
    module("brotli4j-${brotli4JVersion}.jar", 'com.aayushatharva.brotli4j', brotli4JVersion) {
        exports('com.aayushatharva.brotli4j')
        exports('com.aayushatharva.brotli4j.common')
        exports('com.aayushatharva.brotli4j.decoder')
        exports('com.aayushatharva.brotli4j.encoder')
    }

    module("dec-${brotliOrgVersion}.jar", 'org.brotli.dec', brotliOrgVersion) {
        exports('org.brotli.dec')
    }
}

tasks.withType(JacocoReport) {
    reports {
        xml.required = isCI
    }

    executionData(tasks.withType(Test))
}

jacoco {
    toolVersion = "0.8.7"
}

task checkGradleVersion {
    doLast {
        def text = project.getResources().text.fromUri("https://services.gradle.org/versions/current").asString()
        def parsedJson = new groovy.json.JsonSlurper().parseText(text)
        def current = new Semver(gradle.getGradleVersion(), SemverType.LOOSE)
        def latest = parsedJson["version"] as String

        if (current.isLowerThan(latest)) {
            logger.lifecycle("Gradle needs update. Current: {}, Latest: {}", current, latest)
        }
    }
}

wrapper {
    gradleVersion = '7.4.1'
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
